apply from: '../constants.gradle'
apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility project.ext.javaVersion
        targetCompatibility project.ext.javaVersion
    }

    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        applicationId "com.github.warren_bank.exoplayer_airplay_receiver"
        versionName project.ext.releaseVersion
        versionCode project.ext.releaseVersionCode

        archivesBaseName += "-$versionName"
    }

    flavorDimensions "languageResources", "decoderExtensions"

    productFlavors {
        withAllLanguageTranslations {
            dimension "languageResources"
        }
        english {
            dimension "languageResources"
        }
        noDecoderExtensions {
            dimension "decoderExtensions"
            buildConfigField "boolean", "USE_DECODER_EXTENSIONS", "false"
        }
        withDecoderExtensions {
            dimension "decoderExtensions"
            buildConfigField "boolean", "USE_DECODER_EXTENSIONS", "true"
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled   true
            proguardFiles = [
                "proguard-rules.txt",
                getDefaultProguardFile('proguard-android.txt')
            ]
        }
        debug {
            debuggable    true
            jniDebuggable true
        }
    }

    splits {
        abi {
            enable gradle.startParameter.taskNames.any { it.toLowerCase().contains("withdecoderextensions") }
            reset()
            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            universalApk false
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError true
    }
}

dependencies {
    compileOnly         'androidx.annotation:annotation:'                         + project.ext.libVersionAndroidxAnnotation          // ( 27 KB) https://mvnrepository.com/artifact/androidx.annotation/annotation
    annotationProcessor 'androidx.annotation:annotation:'                         + project.ext.libVersionAndroidxAnnotation

    implementation      'androidx.appcompat:appcompat:'                           + project.ext.libVersionAndroidxAppCompat           // (1.0 MB) https://mvnrepository.com/artifact/androidx.appcompat/appcompat
    implementation      'androidx.core:core:'                                     + project.ext.libVersionAndroidxCore                // (665 KB) https://mvnrepository.com/artifact/androidx.core/core
    implementation      'androidx.media:media:'                                   + project.ext.libVersionAndroidxMedia               // (307 KB) https://mvnrepository.com/artifact/androidx.media/media

    implementation      'com.google.android.material:material:'                   + project.ext.libVersionAndroidxMaterial            // (567 KB) https://mvnrepository.com/artifact/com.google.android.material/material

    implementation      'com.google.android.exoplayer:exoplayer-core:'            + project.ext.libVersionExoPlayer                   // (1.3 MB) https://mvnrepository.com/artifact/com.google.android.exoplayer/exoplayer-core?repo=google
    implementation      'com.google.android.exoplayer:exoplayer-dash:'            + project.ext.libVersionExoPlayer                   // (118 KB) https://mvnrepository.com/artifact/com.google.android.exoplayer/exoplayer-dash?repo=google
    implementation      'com.google.android.exoplayer:exoplayer-hls:'             + project.ext.libVersionExoPlayer                   // (146 KB) https://mvnrepository.com/artifact/com.google.android.exoplayer/exoplayer-hls?repo=google
    implementation      'com.google.android.exoplayer:exoplayer-rtsp:'            + project.ext.libVersionExoPlayer                   // (101 KB) https://mvnrepository.com/artifact/com.google.android.exoplayer/exoplayer-rtsp?repo=google
    implementation      'com.google.android.exoplayer:exoplayer-smoothstreaming:' + project.ext.libVersionExoPlayer                   // ( 44 KB) https://mvnrepository.com/artifact/com.google.android.exoplayer/exoplayer-smoothstreaming?repo=google
    implementation      'com.google.android.exoplayer:exoplayer-ui:'              + project.ext.libVersionExoPlayer                   // (433 KB) https://mvnrepository.com/artifact/com.google.android.exoplayer/exoplayer-ui?repo=google
    implementation      'com.google.android.exoplayer:extension-mediasession:'    + project.ext.libVersionExoPlayer                   // ( 80 KB) https://mvnrepository.com/artifact/com.google.android.exoplayer/extension-mediasession?repo=google
    implementation      'com.google.android.exoplayer:extension-rtmp:'            + project.ext.libVersionExoPlayer                   // (  5 KB) https://mvnrepository.com/artifact/com.google.android.exoplayer/extension-rtmp?repo=google

    implementation      'com.googlecode.plist:dd-plist:'                          + project.ext.libVersionDdPlist                     // ( 74 KB) https://mvnrepository.com/artifact/com.googlecode.plist/dd-plist
    implementation      'org.apache.httpcomponents:httpcore:'                     + project.ext.libVersionHttpCore                    // (320 KB) https://mvnrepository.com/artifact/org.apache.httpcomponents/httpcore
    implementation      'org.jmdns:jmdns:'                                        + project.ext.libVersionJmDNS                       // (209 KB) https://mvnrepository.com/artifact/org.jmdns/jmdns

    withDecoderExtensionsImplementation fileTree(dir: "libs/decoder_extensions", include: ["extension-*.aar"])
}

apply plugin: 'com.google.android.gms.strict-version-matcher-plugin'
