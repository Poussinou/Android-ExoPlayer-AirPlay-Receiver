apply from: '../constants.gradle'
apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility project.ext.javaVersion
        targetCompatibility project.ext.javaVersion
    }

    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        applicationId "com.github.warren_bank.exoplayer_airplay_receiver"
        versionName project.ext.releaseVersion
        versionCode project.ext.releaseVersionCode

        archivesBaseName += "-$versionName"
    }

    flavorDimensions "languageResources", "decoderExtensions"

    productFlavors {
        withAllLanguageTranslations {
            dimension "languageResources"
        }
        english {
            dimension "languageResources"
        }
        noDecoderExtensions {
            dimension "decoderExtensions"
            buildConfigField "boolean", "USE_DECODER_EXTENSIONS", "false"
        }
        withDecoderExtensions {
            dimension "decoderExtensions"
            buildConfigField "boolean", "USE_DECODER_EXTENSIONS", "true"
        }
    }

    buildTypes {
        release {
            debuggable      false
            jniDebuggable   false
            minifyEnabled   true
            shrinkResources true
            proguardFiles = [
                "proguard-rules.txt",
                getDefaultProguardFile('proguard-android-optimize.txt')
            ]
        }
        debug {
            debuggable      true
            jniDebuggable   true
            minifyEnabled   true
            shrinkResources false
            proguardFiles = [
                "proguard-rules.debug.txt",
                "proguard-rules.txt",
                getDefaultProguardFile('proguard-android-optimize.txt')
            ]
        }
    }

    splits {
        abi {
            enable gradle.startParameter.taskNames.any { it.toLowerCase().contains("withdecoderextensions") }
            reset()
            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            universalApk false
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError true
    }
}

dependencies {
    compileOnly         'androidx.annotation:annotation:'                   + project.ext.libVersionAndroidxAnnotation          // ( 27 KB) https://mvnrepository.com/artifact/androidx.annotation/annotation
    annotationProcessor 'androidx.annotation:annotation:'                   + project.ext.libVersionAndroidxAnnotation

    implementation      'androidx.appcompat:appcompat:'                     + project.ext.libVersionAndroidxAppCompat           // (1.0 MB) https://mvnrepository.com/artifact/androidx.appcompat/appcompat
    implementation      'androidx.core:core:'                               + project.ext.libVersionAndroidxCore                // (665 KB) https://mvnrepository.com/artifact/androidx.core/core
    implementation      'androidx.media:media:'                             + project.ext.libVersionAndroidxMedia               // (307 KB) https://mvnrepository.com/artifact/androidx.media/media

    implementation      'com.google.android.material:material:'             + project.ext.libVersionAndroidxMaterial            // (567 KB) https://mvnrepository.com/artifact/com.google.android.material/material

    implementation      'androidx.media3:media3-common:'                    + project.ext.libVersionAndroidxMedia3              // (360 KB) https://mvnrepository.com/artifact/androidx.media3/media3-common?repo=google
    implementation      'androidx.media3:media3-database:'                  + project.ext.libVersionAndroidxMedia3              // (  7 KB) https://mvnrepository.com/artifact/androidx.media3/media3-database?repo=google
    implementation      'androidx.media3:media3-datasource:'                + project.ext.libVersionAndroidxMedia3              // (136 KB) https://mvnrepository.com/artifact/androidx.media3/media3-datasource?repo=google
    implementation      'androidx.media3:media3-datasource-rtmp:'           + project.ext.libVersionAndroidxMedia3              // (  4 KB) https://mvnrepository.com/artifact/androidx.media3/media3-datasource-rtmp?repo=google
    implementation      'androidx.media3:media3-decoder:'                   + project.ext.libVersionAndroidxMedia3              // ( 16 KB) https://mvnrepository.com/artifact/androidx.media3/media3-decoder?repo=google
    implementation      'androidx.media3:media3-exoplayer:'                 + project.ext.libVersionAndroidxMedia3              // (1.1 MB) https://mvnrepository.com/artifact/androidx.media3/media3-exoplayer?repo=google
    implementation      'androidx.media3:media3-exoplayer-dash:'            + project.ext.libVersionAndroidxMedia3              // (127 KB) https://mvnrepository.com/artifact/androidx.media3/media3-exoplayer-dash?repo=google
    implementation      'androidx.media3:media3-exoplayer-hls:'             + project.ext.libVersionAndroidxMedia3              // (155 KB) https://mvnrepository.com/artifact/androidx.media3/media3-exoplayer-hls?repo=google
    implementation      'androidx.media3:media3-exoplayer-rtsp:'            + project.ext.libVersionAndroidxMedia3              // (145 KB) https://mvnrepository.com/artifact/androidx.media3/media3-exoplayer-rtsp?repo=google
    implementation      'androidx.media3:media3-exoplayer-smoothstreaming:' + project.ext.libVersionAndroidxMedia3              // ( 45 KB) https://mvnrepository.com/artifact/androidx.media3/media3-exoplayer-smoothstreaming?repo=google
    implementation      'androidx.media3:media3-extractor:'                 + project.ext.libVersionAndroidxMedia3              // (694 KB) https://mvnrepository.com/artifact/androidx.media3/media3-extractor?repo=google
    implementation      'androidx.media3:media3-session:'                   + project.ext.libVersionAndroidxMedia3              // (442 KB) https://mvnrepository.com/artifact/androidx.media3/media3-session?repo=google
    implementation      'androidx.media3:media3-ui:'                        + project.ext.libVersionAndroidxMedia3              // (422 KB) https://mvnrepository.com/artifact/androidx.media3/media3-ui?repo=google

    implementation      'com.googlecode.plist:dd-plist:'                    + project.ext.libVersionDdPlist                     // ( 74 KB) https://mvnrepository.com/artifact/com.googlecode.plist/dd-plist
    implementation      'org.apache.httpcomponents:httpcore:'               + project.ext.libVersionHttpCore                    // (320 KB) https://mvnrepository.com/artifact/org.apache.httpcomponents/httpcore
    implementation      'org.jmdns:jmdns:'                                  + project.ext.libVersionJmDNS                       // (209 KB) https://mvnrepository.com/artifact/org.jmdns/jmdns

    withDecoderExtensionsImplementation fileTree(dir: "libs/decoder_extensions", include: ["extension-*.aar"])
}

apply plugin: 'com.google.android.gms.strict-version-matcher-plugin'
